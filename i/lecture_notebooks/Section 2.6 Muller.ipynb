{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "d5b9bce4-3cda-486d-830e-160d57debf5a",
   "metadata": {},
   "source": [
    "# Muller's Method for Complex Roots"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ac081d6e-7ec8-4994-b910-058dfdeeec2a",
   "metadata": {},
   "outputs": [],
   "source": [
    "function newton(f, df, p0, n_max, rel_tol; verbose = true)\n",
    "    \n",
    "    converged = false;\n",
    "    p = p0;\n",
    "    p_old = p0;\n",
    "\n",
    "    for i in 1:n_max\n",
    "\n",
    "        p = p_old - f(p_old)/df(p_old);\n",
    "        \n",
    "        if verbose\n",
    "            println(\" $i: p = $(p), |f(p)| = $(abs(f(p)))\")\n",
    "        end\n",
    "\n",
    "        if (i>1)\n",
    "            if abs(p-p_old)/abs(p)< rel_tol\n",
    "                converged = true;\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "\n",
    "        p_old = p;\n",
    "\n",
    "    end\n",
    "    \n",
    "    if !converged\n",
    "        println(\"ERROR: Did not converge after $n_max iterations\")\n",
    "    end\n",
    "\n",
    "    return p\n",
    "    \n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2f8727f3-417e-432c-b5e5-3bef53dee5aa",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    muller(f, p0, p1, p2, n_max, rel_tol; verbose = true)\n",
    "\n",
    "Find a root of `f` using Muller's method (quadratic interpolation).\n",
    "\n",
    "Arguments\n",
    "- `f` : function (supports complex values)\n",
    "- `p0,p1,p2` : three initial guesses (may be complex)\n",
    "- `n_max` : maximum number of iterations\n",
    "- `rel_tol` : relative tolerance for convergence: |p_{n+1}-p_n|/|p_{n+1}| < rel_tol\n",
    "- `verbose` : if true, print iteration info (default: true)\n",
    "\n",
    "Returns\n",
    "- `p` : approximate root\n",
    "\n",
    "Description\n",
    "Muller's method fits a quadratic through (p0,f(p0)), (p1,f(p1)), (p2,f(p2)), solves that\n",
    "quadratic for a root and chooses the root that gives a larger denominator to reduce\n",
    "subtractive cancellation. The triple (p1,p2,p) is then used for the next iteration.\n",
    "The method naturally handles complex arithmetic and can find complex roots starting\n",
    "from real initial guesses.\n",
    "\n",
    "Notes\n",
    "- Convergence is typically superlinear (order ≈ 1.84) but not guaranteed for every\n",
    "  function or choice of starting points.\n",
    "- Be mindful of nearly zero denominators; using complex arithmetic (Julia's built-in)\n",
    "  avoids issues with negative discriminant.\n",
    "- If the algorithm fails to converge within `n_max` iterations, it returns the last\n",
    "  iterate and prints an error message (unless `verbose` is false).\n",
    "\n",
    "Example\n",
    "```julia\n",
    "p = muller(x->x^3 - 1, -1.0, 0.0, 1.5, 100, 1e-8)\n",
    "```\n",
    "\"\"\"\n",
    "function muller(f, p0, p1, p2, n_max, rel_tol; verbose = true)\n",
    "    \n",
    "    converged = false;\n",
    "    p = p2;\n",
    "\n",
    "    for i in 1:n_max\n",
    "\n",
    "        # solve for the constants a, b, and c\n",
    "        c = f(p2);\n",
    "        A = [(p0-p2)^2 p0-p2; (p1-p2)^2 p1-p2 ]; # builds matrix\n",
    "        x = A\\[f(p0)-c; f(p1)-c]; # solves for a and b\n",
    "        a = x[1];\n",
    "        b = x[2];\n",
    "        \n",
    "        # take the root with larger denominator\n",
    "        if abs(b + sqrt(b^2-4*a*c))> abs(b - sqrt(b^2-4*a*c))\n",
    "            p = p2 - 2*c/(b + sqrt(b^2-4*a*c));\n",
    "        else\n",
    "            p = p2 - 2*c/(b - sqrt(b^2-4*a*c));            \n",
    "        end\n",
    "        \n",
    "        if verbose\n",
    "            println(\" $i: p = $(p), |f(p)| = $(abs(f(p)))\")\n",
    "        end\n",
    "        \n",
    "        if (i>1)\n",
    "            if abs(p-p2)/abs(p)< rel_tol\n",
    "                converged = true;\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "\n",
    "        # update entries\n",
    "        p0 = p1;\n",
    "        p1 = p2;\n",
    "        p2 = p;\n",
    "\n",
    "    end\n",
    "    \n",
    "    if !converged\n",
    "        println(\"ERROR: Did not converge after $n_max iterations\")\n",
    "    end\n",
    "\n",
    "    return p\n",
    "    \n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "03b5a74a-49fb-49f6-9a39-041a93834928",
   "metadata": {},
   "source": [
    "## Example 1\n",
    "Find a complex root of\n",
    "$$\n",
    "x^3 -1\n",
    "$$\n",
    "With an appropriate starting guess, Newton can find the complex roots.  But the starting guess will need to be complex."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f108ab84",
   "metadata": {},
   "source": [
    "In Julia, `im` is the imaginary number $i$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7954775d-0dbb-4454-a832-f6d82d9a7915",
   "metadata": {},
   "outputs": [],
   "source": [
    "f = x-> x^3 - 1;\n",
    "df = x->3*x^2;\n",
    "p0 = 0. + 1. *im; # this leads to a complex root\n",
    "# p0 =  2. + 1. *im;  # this leads to the real root\n",
    "rel_tol = 1e-12;\n",
    "n_max = 100;\n",
    "\n",
    "p = newton(f, df, p0, n_max, rel_tol);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "00610991-fa87-4b02-9944-009eebc24249",
   "metadata": {},
   "outputs": [],
   "source": [
    "@show (-1 + sqrt(3)*im)/2;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8c4c1a24",
   "metadata": {},
   "source": [
    "Try Muller's method:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9b523377-c5b6-4358-9c70-8b8688a16d6b",
   "metadata": {},
   "outputs": [],
   "source": [
    "f = x-> x^3 - 1;\n",
    "p0 = -1. + 1*im; # finds a complex root\n",
    "p1 = 0. + 1*im; \n",
    "p2 = 0. + 2*im;\n",
    "\n",
    "# p0 = -2.0 + 0 * im; # finds a real root\n",
    "# p1 = -1.0 + 0 * im;\n",
    "# p2 = 1.5 + 0.1 * im;\n",
    "\n",
    "rel_tol = 1e-8;\n",
    "n_max = 100;\n",
    "\n",
    "p = muller(f, p0, p1, p2, n_max, rel_tol);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c1892c00",
   "metadata": {},
   "outputs": [],
   "source": [
    "p"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "58807ea9-3528-45a8-9738-5c33235ecb5f",
   "metadata": {},
   "source": [
    "## Example 2\n",
    "Find a complex root of\n",
    "$$\n",
    "x^4 - 3x^3+x^2+x+1\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d2f037ad-06b5-4202-a49e-347efc71d8a5",
   "metadata": {},
   "outputs": [],
   "source": [
    "f = x-> x^4-3*x^3 +x^2 +x + 1;\n",
    "p0 = 0.5 + 0*im;\n",
    "p1 = -0.5 + 0*im;\n",
    "p2 = 0.0 + 0*im;\n",
    "\n",
    "rel_tol = 1e-8;\n",
    "n_max = 100;\n",
    "\n",
    "p = muller(f, p0, p1, p2, n_max, rel_tol);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c9a16c5b",
   "metadata": {},
   "source": [
    "## Example 3\n",
    "Find a  root of\n",
    "$$\n",
    "e^x + 1 = 0\n",
    "$$\n",
    "**NOTE** this is a not a polynomial."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "472c8fe6",
   "metadata": {},
   "outputs": [],
   "source": [
    "f = x-> exp(x) + 1;\n",
    "p0 = 0.5 + 0*im;\n",
    "p1 = -0.5 + 0*im;\n",
    "p2 = 1.0 + 0*im;\n",
    "\n",
    "rel_tol = 1e-8;\n",
    "n_max = 100;\n",
    "\n",
    "p = muller(f, p0, p1, p2, n_max, rel_tol);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6accc3aa",
   "metadata": {},
   "outputs": [],
   "source": [
    "@show abs(f(-π * im));"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "053d52cd",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.0",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
